import java.util.stream.Collectors

plugins {
    id "maven-publish"
    id "org.jetbrains.kotlin.jvm" version "1.6.20"
    id "org.jetbrains.kotlin.kapt" version "1.6.20"
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

allprojects {
    group "gg.scala.lemon"
    version "1.1.1"

    repositories {
        mavenCentral()

        maven {
            name "Scala"
            allowInsecureProtocol true
            url "http://repository.scala.gg/repository/maven-releases/"

            credentials {
                username property("scala.credentials.username") as String
                password property("scala.credentials.password") as String
            }
        }

        maven { url 'https://jitpack.io' }

        maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
        maven { url "https://repo.dmulloy2.net/repository/public/" }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }

        maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    }
}

subprojects {
    apply plugin: "maven-publish"
    apply plugin: "org.jetbrains.kotlin.jvm"
    apply plugin: "org.jetbrains.kotlin.kapt"
    apply plugin: "com.github.johnrengelman.shadow"

    compileKotlin {
        kotlinOptions.javaParameters = true
        kotlinOptions.jvmTarget = "1.8"
    }

    shadowJar {
        archiveClassifier.set("")

        exclude "**/*.kotlin_metadata"
        exclude "**/*.kotlin_builtins"
        exclude "META-INF/"

        // we don't want kotlin
        exclude "kotlin"

        // helper-redis assets
        exclude "config.yml"

        archiveFileName = "Lemon.jar"
    }

    tasks.withType(GenerateMavenPom).all {
        doLast {
            final File file = new File(
                    "$buildDir/publications/shadow/pom-default.xml"
            )

            final BufferedReader br = new BufferedReader(
                    new FileReader(file)

            )
            final String lines = br.lines()
                    .map((line) -> {
                        if (line.contains("<dependencies/>")) "" else line
                    })
                    .collect(Collectors.joining("\n"))

            final BufferedWriter wr = new BufferedWriter(
                    new FileWriter(file, true)
            )

            final PrintWriter writer = new PrintWriter(file)
            writer.print("")
            writer.close()

            wr.write(lines)
            wr.close()
        }
    }

    publishing {
        publications {
            shadow(MavenPublication) { publication ->
                project.shadow.component(publication)
            }
        }

        repositories {
            maven {
                name "Scala"
                allowInsecureProtocol true
                url "http://repository.scala.gg/repository/maven-releases/"

                credentials {
                    username property("scala.credentials.username") as String
                    password property("scala.credentials.password") as String
                }
            }
        }
    }

    tasks.build.dependsOn(shadowJar, publishShadowPublicationToScalaRepository)
}
